DEFINE_ACTION_FUNCTION winter_wolf_breath
BEGIN
  spl.make[WINTER_WOLF_BREATH]
  [
    m_name := @0
    m_type := "innate"
    m_level = 1
    m_hostile = 1
    m_ignore_dead_magic = 1
    m_ignore_wild_surge = 1
    m_can_target_invisible = 1
    m_castable_when_silenced = 1

    m.ab.add{s_level = 1;;s_type = 2;;s_location = 4;;s_target = 1;;s_range = 10;;s_casting_time = 3;;s_projectile = 250}

    PATCH_IF enhanced_edition
    BEGIN
      m.ab_fx.add{s_opcode = 12;;s_target = 2;;s_timing = 1;;s_parameter2 = 0x00020000;;s_dicenumber = 6;;s_dicesize = 4;;s_save_vs_breath = 1;;s_save_for_half = 1}
    END
    ELSE
    BEGIN
      m.ab_fx.add{s_opcode = 12;;s_target = 2;;s_timing = 1;;s_parameter2 = 0x00020000;;s_dicenumber = 3;;s_dicesize = 4}
      m.ab_fx.add{s_opcode = 12;;s_target = 2;;s_timing = 1;;s_parameter2 = 0x00020000;;s_dicenumber = 3;;s_dicesize = 4;;s_save_vs_breath = 1}
    END
  ]

  itm.copy[p2-8=>mh#wntwm]
  [
    m_magical = 1
    m_enchantment = 1
    m.ab_fx.add{s_opcode = 12;;s_target = 2;;s_timing = 1;;s_parameter2 = 0x00020000;;s_dicenumber = 2;;s_dicesize = 4}
  ]

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "wntrwolf.ssl"
    location	= "scripts"
  END

  COPY_EXISTING_REGEXP "^.+\.cre$" "override"
    LPF struct_get STR_VAR arguments = "race" RET race_num = value END
    LPF struct_get STR_VAR arguments = "class" RET class_num = value END

    LOOKUP_IDS_SYMBOL_OF_INT race "race" race_num
    LOOKUP_IDS_SYMBOL_OF_INT class "class" class_num
    
    TO_LOWER race
    TO_LOWER class

    PATCH_IF "%race%" == "wolf"
         AND "%class%" == "wolf_winter"
    BEGIN
      LPF struct_read RET_ARRAY m = struct END
      m.cre_add_items{mh#wntwm(weapon1) mh#wntwm(weapon2)|replace:i=1}
      m.cre_strip_scripts{all}
      m_script_race := "WNTRWOLF"
      m_script_default := "WTASIGHT"
      LPF struct_write STR_VAR struct = "m" END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// winter_wolf_breath


