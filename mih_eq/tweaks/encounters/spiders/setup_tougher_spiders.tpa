DEFINE_ACTION_FUNCTION setup_tougher_spiders
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "spider_adjustment"
    RET
    spider_adjustment	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "spider_resistance"
    RET
    spider_resistance	= value
  END

  LAUNCH_ACTION_FUNCTION 2da_read
    STR_VAR
    file	= "spiders.2da"
    path	= "%MOD_FOLDER%/%component_loc%"
    type	= "2da"
    case	= "lower"
    RET_ARRAY
    spiders	= array
    types	= rows
    stats	= columns
  END


  COPY_EXISTING_REGEXP "^.+\.cre$" "override"
    LPF struct_get STR_VAR arguments = "race" RET race_num = value END
    LPF struct_get STR_VAR arguments = "class" RET class_num = value END

    LOOKUP_IDS_SYMBOL_OF_INT race "race" race_num
    LOOKUP_IDS_SYMBOL_OF_INT class "class" class_num
    
    TO_LOWER race
    TO_LOWER class

    PATCH_IF "%race%" STRING_EQUAL "spider"
         AND VARIABLE_IS_SET $types("%class%")
    BEGIN
      PATCH_MATCH "%SOURCE_RES%"
      WITH
        "bdspidga"
	BEGIN
	  TEXT_SPRINT class "spider_gargantuan"
	END
        "spidphas"
        BEGIN
          TEXT_SPRINT class "spider_astral_phase"
        END
        "spidvo.."
        BEGIN
	  TEXT_SPRINT class "spider_vortex"
	END
	DEFAULT
      END

      LPF struct_read RET_ARRAY m = struct END
      PHP_EACH stats AS stat=>discard
      BEGIN
        PATCH_MATCH "%stat%"
        WITH
          "hp"
	  BEGIN
	    SET $m("%stat%") = $spiders("%class%" "%stat%") + (m_level1 * spider_adjustment)
	  END
	  "thac0" "ac"
	  BEGIN
	    SET $m("%stat%") = $spiders("%class%" "%stat%") - spider_adjustment
	  END
	  DEFAULT
	    SET $m("%stat%") = $spiders("%class%" "%stat%")
        END

	SET m_resist_slashing = spider_resistance
	SET m_resist_crushing = spider_resistance
	SET m_resist_piercing = spider_resistance
	SET m_resist_missile = spider_resistance
      END
      LPF struct_write STR_VAR struct = "m" END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// setup_tougher_spiders


