DEFINE_ACTION_FUNCTION tsolak_battle
BEGIN
  MAKE_PATCH
    strength=>18
    strength_ex=>100
    constitution=>18
    dexterity=>18
    intelligence=>18
    charisma=>17
    xp_value=>12500
    hitpoints=>89
    ac=>"-1"
    level1=>11
    enforce_thac0=>at_worst
    enforce_saves=>at_worst
    attacks=>2
    resist_magic=>20
    replace_items=>"vampreg1(lring) immune2(rring)"
  END   

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature		= "bdtsolak"
    edits		= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature		= "bdtsolak=>mh#ts2nd"
    editstring		= ~script_override=>"mh#ts2nd" dialog=>"none" allegiance=>"enemy"~
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature		= "bdisabel"
    editstring		= ~replace_items=>"sw1h41(weapon1)"~
  END

  MAKE_PATCH
    xp_value=>0		// Summoned, already part of vampire XP
    hitpoints=>"hp_max + level1 * 3"
    thac0=>"thac0 - 3"
    attacks=>6
    save_vs_death=>"save_vs_death - 3"
    save_vs_wands=>"save_vs_wands - 3"
    save_vs_polymorph=>"save_vs_polymorph - 3"
    save_vs_breath=>"save_vs_breath - 3"
    save_vs_spells=>"save_vs_spells - 3"
    ac=>"ac_natural - 3"
    replace_items=>"p1-12(weapon1)"
  END

  LAUNCH_ACTION_FUNCTION clone_creature
    STR_VAR
    creature		= "wolfdi=>mh#tswlf"
    edits		= "patch_data"
  END

  COPY_EXISTING "bd7000.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH "wolfdi" "mh#tswlf"
    END

  LAUNCH_ACTION_FUNCTION extend
    STR_VAR
    script	= "bdtsolak"
    top		= "spawn_tsolak_shadows"
    location	= "scripts"
  END

  LAUNCH_ACTION_FUNCTION extend
    STR_VAR
    script	= "bdcof01"
    top		= "tsolak_2nd_wind"
    location	= "scripts"
  END

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "mh#ts2nd.baf"
    location	= "scripts"
  END

  MAKE_PATCH
    add_actor_inline'1=>~actor_name=>"{literal:Wight}" actor_resource=>"mh#wght6" actor_x_coord=>245 actor_y_coord=>150 actor_orientation=>2~
    add_actor_inline'2=>~actor_name=>"{literal:Wight}" actor_resource=>"mh#wght6" actor_x_coord=>280 actor_y_coord=>200 actor_orientation=>1~
    add_actor_inline'3=>~actor_name=>"{literal:Wight}" actor_resource=>"mh#wght6" actor_x_coord=>460 actor_y_coord=>125 actor_orientation=>3~
    add_actor_inline'3=>~actor_name=>"{literal:Wight}" actor_resource=>"mh#wght7" actor_x_coord=>445 actor_y_coord=>200 actor_orientation=>4~
  END

  LAUNCH_ACTION_FUNCTION edit_area
    STR_VAR
    area		= "bd0115"
    edits		= "patch_data"
  END
END	// tsolak_battle


