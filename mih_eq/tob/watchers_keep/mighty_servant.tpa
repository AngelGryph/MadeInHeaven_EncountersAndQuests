DEFINE_ACTION_FUNCTION mighty_servant
BEGIN
  // Create innate abilities from spells

  LAUNCH_ACTION_FUNCTION add_spell_to_ids
    INT_VAR
    level	= 5
    STR_VAR
    typename	= "innate"
    idsname	= "LEUKO_FLAME_STRIKE"
    RET
    newname
  END
  
  MAKE_PATCH
    spell_type=>"innate"
    delete_ability=>~ability_min_level < 20~
    patch_ability_inline=>~ability_min_level=>1 ability_icon_loc=>4 casting_time=>1~
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "sppr503=>%newname%"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION add_spell_to_ids
    INT_VAR
    level	= 6
    STR_VAR
    typename	= "innate"
    idsname	= "LEUKO_CHAIN_LIGHTNING"
    RET
    newname
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "spwi615=>%newname%"
    edits	= "patch_data"
  END


  // Install pseudo-weapon

  LAUNCH_ACTION_FUNCTION install_item
    STR_VAR
    item	= "mh#leuko"
    location	= "items"
  END


  // Compile script

  LAUNCH_ACTION_FUNCTION ssl_to_bcs
    STR_VAR
    script	= "mh#leuko"
    location	= "scripts"
    locbase
  END


  // Install creature file

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "infinity_animations"
    RET
    infinity_animations	= value
  END

  ACTION_IF infinity_animations
  BEGIN
    OUTER_TEXT_SPRINT mighty_servant "iron_golem_iwd"
  END

  ACTION_IF enhanced_edition
  BEGIN
    OUTER_TEXT_SPRINT mighty_servant "golem_iron_iwd"
  END

  ACTION_IF NOT VARIABLE_IS_SET dragon_white
  BEGIN
    OUTER_TEXT_SPRINT mighty_servant "no_change"
  END

  LAUNCH_ACTION_FUNCTION install_creature
    STR_VAR
    creature	= "mh#leuko"
    location	= "actors"
    editstring	= ~say_both_names=>0 animation=>"%mighty_servant%"~
  END


  // Note: Actual placement in game done in machine_ltm.tpa
END	// mighty_servant


