DEFINE_ACTION_FUNCTION vongoethe_battle
BEGIN
  // Salient ability: Doom Gaze

  LAUNCH_ACTION_FUNCTION add_spell_to_ids
    INT_VAR
    level	= 7
    STR_VAR
    typename	= "innate"
    idsname	= "VONGOETHE_DOOM_GAZE"
    RET
    newname
  END

  MAKE_PATCH
    delete_effect=>"opcode is_in [171 172]"
    patch_effect_inline=>~match=>"save_vs_death = 1" save_bonus=>"-6"~
    add_effect_inline=>~opcode=>12 parameter2=>0x00400000 dicenum=>2 dicesize=>10 resist_dispel=>1~
    say_name=>0
  END

  LAUNCH_ACTION_FUNCTION clone_spell
    STR_VAR
    spell	= "spin991=>%newname%"
    edits	= "patch_data"
  END


  // Vongoethe patches

  MAKE_PATCH
    // Salient Ability: Regeneration (18/round)
    add_effect_global_inline'1=>~opcode=>98 timing=>2 parameter1=>3 parameter2=>2~
    // Salient Ability: Ignore Metal
    delete_effect_global=>"opcode is_in [86 87 88 89]"
    add_effect_global_inline'2=>~number_to_add=>4 opcode=>"entry_index from [86 87 88 89]" timing=>2 parameter1=>75 parameter2=>1~
    // Power Ritual: Personal Protection (+4 or better)
    add_effect_global_inline'2=>~number_to_add=>3 opcode=>120 timing=>2 parameter1=>"entry_index + 1"~
  END

  LAUNCH_ACTION_FUNCTION clone_item
    STR_VAR
    item	= "lich=>mh#vgoet"
    edits	= "patch_data"
  END

  LAUNCH_ACTION_FUNCTION ssl_to_bcs
    STR_VAR
    script	= "mh#vgoet"
    location	= "scripts"
    locbase
  END

  MAKE_PATCH
    xp_value=>0
    replace_items=>"mh#vgoet(rring) minhp1(amulet)"
    insert_script_high=>"mh#vgoet"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "amlich01"
    edits	= "patch_data"
  END


  // Script patches

  COPY_EXISTING "ar5509.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
       ~skelwa01~ ~mh#skw15~

      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
       ~banshe01~ ~mh#eyeff~
    END

  COPY_EXISTING "amlich01.bcs" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH
       ~skelar02~ ~mh#bodak~
    END
END	// vongoethe_battle


