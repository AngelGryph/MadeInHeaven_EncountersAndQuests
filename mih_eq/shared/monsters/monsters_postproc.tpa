DEFINE_ACTION_FUNCTION monsters_postproc
BEGIN
  // Black Pudding armor damaged message

  OUTER_SET armor_damaged = RESOLVE_STR_REF (@34)

  MAKE_PATCH
    patch_effect_inline=>~match=>"opcode = 139" parameter1=>%armor_damaged%~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#blkpd"
    edits	= "patch_data"
    location	= "items"
    locbase
  END


  // Wight level drain messages

  ACTION_IF enhanced_edition AND is_bg1 AND NOT is_bg2
  BEGIN
    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 216" opcode=>139 parameter1=>"(parameter1 - 1) from [25802 25803 25804 25805 25806]"~
    END
  END
  ELSE
  BEGIN
    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 216" opcode=>139 parameter1=>"(parameter1 - 1) from [41495 40968 40969 40979 41616]"~
    END
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#bwght mh#wight"
    edits	= "patch_data"
  END


  // Set appropriate animations for some creatures

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "infinity_animations"
    RET
    infinity_animations	= value
  END

  ACTION_IF infinity_animations
  BEGIN
    OUTER_TEXT_SPRINT animation_style "ia"
  END

  ACTION_IF enhanced_edition
  BEGIN
    OUTER_TEXT_SPRINT animation_style "ee"
  END

  ACTION_IF NOT VARIABLE_IS_SET animation_style
  BEGIN
    OUTER_TEXT_SPRINT animation_style "fallback"
  END

  COPY - "%MOD_FOLDER%/shared/monsters/tables/animations.2da" "%workspace%"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    FOR (SET i = 0; i < rows; ++i)
    BEGIN
      LAUNCH_PATCH_FUNCTION read_table_entry
        INT_VAR
        rownum		= i
        colnum		= 0
        RET
        creature	= value
      END
      
      LAUNCH_PATCH_FUNCTION read_table_entry
        INT_VAR
        rownum		= i
	STR_VAR
	column		= "%animation_style%"
        RET
        animation	= value
      END

      PATCH_IF IDS_OF_SYMBOL("animate" "%animation%") == "-1"
      BEGIN
        PATCH_FAIL ~Animation %animation% not found for creature %creature%!~
      END

      INNER_ACTION
      BEGIN
        LAUNCH_ACTION_FUNCTION edit_creature
          STR_VAR
	  creature
	  editstring	= ~animation=>"%animation%"~
        END
      END
    END
END	// monsters_postproc


