DEFINE_ACTION_FUNCTION monsters_postproc
BEGIN
  // Black Pudding armor damaged message

  OUTER_SET armor_damaged = RESOLVE_STR_REF (@34)

  MAKE_PATCH
    patch_effect_inline=>~match=>"opcode = 139" parameter1=>%armor_damaged%~
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#blkpd"
    edits	= "patch_data"
    location	= "items"
    locbase
  END


  // Level drain messages

  ACTION_IF enhanced_edition AND is_bg1 AND NOT is_bg2
  BEGIN
    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 216" opcode=>139 parameter1=>"(parameter1 - 1) from [25802 25803 25804 25805 25806]"~
    END
  END
  ELSE
  BEGIN
    MAKE_PATCH
      clone_effect_inline=>~match=>"opcode = 216" opcode=>139 parameter1=>"(parameter1 - 1) from [41495 40968 40969 40979 41616]"~
    END
  END

  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item	= "mh#bwght mh#wight"
    edits	= "patch_data"
  END


  // Set appropriate animations for some creatures

  LAUNCH_ACTION_FUNCTION process_table
    STR_VAR
    table	= "animations.2da"
    function	= "__animation_helper"
    location	= "tables"
    locbase
  END
END	// monsters_postproc


DEFINE_ACTION_FUNCTION __animation_helper
  STR_VAR
  RESREF	= ~~
  EE		= ~~
  IA		= ~~
  FALLBACK	= ~~
BEGIN
  OUTER_TEXT_SPRINT animation	""

  ACTION_IF IDS_OF_SYMBOL("animate" "%FALLBACK%") != "-1"
  BEGIN
    OUTER_TEXT_SPRINT animation "%FALLBACK%"
  END

  ACTION_IF IDS_OF_SYMBOL("animate" "%IA%") != "-1"
  BEGIN
    OUTER_TEXT_SPRINT animation "%IA%"
  END

  ACTION_IF IDS_OF_SYMBOL("animate" "%EE%") != "-1"
  BEGIN
    OUTER_TEXT_SPRINT animation "%EE%"
  END

  ACTION_IF "%animation%" STRING_EQUAL ""
  BEGIN
    FAIL ~No suitable animation found for creature %RESREF%, tried %EE%, %IA% and %FALLBACK%!~
  END

  LAUNCH_ACTION_FUNCTION log_this
  STR_VAR
    file	= "animations.txt"
    input	= ~Using animation "%animation% for creature "%RESREF%".~
    repeat	= "no"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    STR_VAR
    creature	= "%RESREF%"
    editstring	= ~animation=>"%animation%"~
  END
END


