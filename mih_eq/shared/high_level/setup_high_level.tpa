// Functions to help creater tougher versions of monsters for high-level adventures.
//
// These are based on the procedures described in the source book "DM's Option: High-Level Campaigns"

DEFINE_ACTION_FUNCTION setup_high_level
BEGIN
END


DEFINE_PATCH_FUNCTION CRE_hp_plus
  STR_VAR
  arguments	= ""
BEGIN
  LAUNCH_PATCH_FUNCTION fail_unless_int
    STR_VAR
    value="%arguments%"
    expression=""
  END

  SET hp_bonus = "%arguments%"
  SET combat_bonus = hp_bonus / 3

  CLEAR_ARRAY hp_plus
  DEFINE_ASSOCIATIVE_ARRAY hp_plus
  BEGIN
    hp_current		=> "hp_current + %hp_bonus%"
    hp_max		=> "hp_max + %hp_bonus%"
    thac0		=> "thac0 - %combat_bonus%"
    save_vs_death	=> "save_vs_death - %combat_bonus%"
    save_vs_wands	=> "save_vs_wands - %combat_bonus%"
    save_vs_polymorph	=> "save_vs_polymorph - %combat_bonus%"
    save_vs_breath	=> "save_vs_breath - %combat_bonus%"
    save_vs_spell	=> "save_vs_spell - %combat_bonus%"
  END
  
  LAUNCH_PATCH_FUNCTION apply_patches
    STR_VAR
    file_ext	= "CRE"
    edits	= "hp_plus"
  END
END


DEFINE_PATCH_FUNCTION CRE_make_legendary
  STR_VAR
  arguments	= ""
BEGIN
  INNER_ACTION
  BEGIN
    LAUNCH_ACTION_FUNCTION read_table_entry_external
      STR_VAR
      file	= "legendary.2da"
      location	= "tables"
      locbase	= "shared/high_level"
      row	= "%arguments%"
      column	= "HD"
      RET
      hd_bonus		= value
    END

    LAUNCH_ACTION_FUNCTION read_table_entry_external
      STR_VAR
      file	= "legendary.2da"
      location	= "tables"
      locbase	= "shared/high_level"
      row	= "%arguments%"
      column	= "AC"
      RET
      ac_bonus		= value
    END

    LAUNCH_ACTION_FUNCTION read_table_entry_external
      STR_VAR
      file	= "legendary.2da"
      location	= "tables"
      locbase	= "shared/high_level"
      row	= "%arguments%"
      column	= "THAC0"
      RET
      thac0_bonus	= value
    END
  END

  CLEAR_ARRAY make_legendary
  DEFINE_ASSOCIATIVE_ARRAY make_legendary
  BEGIN
    level_all=>"level1 + %hd_bonus%"
    ac=>"if (ac_effective - %ac_bonus% < -20) then -20 else (ac_effective - %ac_bonus%)"
    thac0=>"thac0 - %thac0_bonus%"
    enforce_hp=>at_worst
    enforce_saves=>at_worst
  END
  
  LAUNCH_PATCH_FUNCTION apply_patches
    STR_VAR
    file_ext	= "CRE"
    edits	= "make_legendary"
  END
END


