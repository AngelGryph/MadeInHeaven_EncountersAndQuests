DEFINE_ACTION_FUNCTION lich_hd_by_spell
BEGIN
  COPY_EXISTING_REGEXP "^.+\.cre" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_race
      RET
      race	= value
    END

    PATCH_IF ("%race%" STRING_EQUAL_CASE "lich")
    BEGIN
      SET wizard_level	= 11

      GET_OFFSET_ARRAY known_spells CRE_V10_KNOWN_SPELLS
      PHP_EACH known_spells AS int => spell_offset
      BEGIN
        READ_SHORT (spell_offset + 0x0008) spell_level
	++spell_level

	PATCH_IF (spell_level > 5 && wizard_level < spell_level * 2)
	BEGIN
	  SET wizard_level = spell_level * 2
	END
      END

      CLEAR_ARRAY patch_data

      DEFINE_ASSOCIATIVE_ARRAY patch_patch
      BEGIN
        level1GT=>"%wizard_level%"
	enforce_hp=>at_worst
	enforce_thac0=>at_worst
	enforce_saves=>at_worst
      END

      LAUNCH_PATCH_FUNCTION CRE_read_xp_value
        RET
	xp_value	= value
      END

      PATCH_IF (xp_value > 0)
      BEGIN
        DEFINE_ASSOCIATIVE_ARRAY patch_data
	BEGIN
	  enforce_xp=>7
	END
      END

      LAUNCH_PATCH_FUNCTION apply_patches
        STR_VAR
	edits		= "patch_data"
	file_ext	= "CRE"
      END
    END

    BUT_ONLY_IF_IT_CHANGES
END	// lich_hd_by_spell


