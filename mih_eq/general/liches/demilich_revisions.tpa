DEFINE_ACTION_FUNCTION demilich_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "demilich_howl_save_penalty"
    RET
    demilich_howl_save_penalty	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "demilich_curse_debuff"
    RET
    demilich_curse_debuff	= value
  END


  // Demiliches resist all kinds of damage

  MAKE_PATCH
    delete_effect_global=>"opcode is_in [27 28 29 30 31 84 85 86 87 88 89 173]"
    add_effect_global_inline=>~number_to_add=>8 opcode=>"entry_index from [27 30 31 84 86 87 88 89]" target=>1 timing=>2 parameter1=>90 parameter2=>1~
    add_effect_global_inline=>~number_to_add=>4 opcode=>"entry_index from [28 29 85 173]" target=>1 timing=>2 parameter1=>100 parameter2=>1~
  END


  // Demilich Howl

  MAKE_PATCH
    patch_effect_inline=>~match=>"save_vs_death = 1" save_bonus=>"-%demilich_howl_save_penalty%"~
  END

  LAUNCH_ACTION_FUNCTION edit_spell
    STR_VAR
    spell	= "DEMILICH_DEATH"
    edits	= "patch_data"
  END


  // Demilich Curse

  LAUNCH_ACTION_FUNCTION make_effect
    STR_VAR
    effect		= "mh#dlcac mh#dlcsd mh#dlcsw mh#dlcsp mh#dlcsb mh#dlcss mh#dlcth"
    editstring		= ~opcode=>"object_index from [0 33 34 35 36 37 278]" timing=>1 parameter1=>"-%demilich_curse_debuff%" power=>9 resist_dispel=>2~
  END

  LAUNCH_ACTION_FUNCTION make_effect
    STR_VAR
    effect		= "mh#dlcst"
    editstring		= ~opcode=>142 timing=>1 parameter2=>35 power=>9 resist_dispel=>2~
  END

  LAUNCH_ACTION_FUNCTION install_spell
    STR_VAR
    spell		= "demilich_curse"
    idsname		= "DEMILICH_CURSE"
    location		= "spells"
    editstring		= "say_name=>0"
  END

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file		= "mh#dlich.baf"
    location		= "scripts"
  END

  LAUNCH_ACTION_FUNCTION edit_creature
    INT_VAR
    allow_missing	= 1
    STR_VAR
    creature		= "hldemi demilich esxlich"
    editstring		= ~insert_script_high=>"mh#dlich"~
  END
END	// demilich_revisions


