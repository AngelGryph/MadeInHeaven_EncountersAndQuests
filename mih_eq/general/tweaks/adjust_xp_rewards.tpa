DEFINE_ACTION_FUNCTION adjust_xp_rewards
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_monsters"
    RET
    xp_percentage_monsters	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_group"
    RET
    xp_percentage_group		= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini		= "xp_percentage_individual"
    RET
    xp_percentage_individual	= value
  END


  MAKE_PATCH
    xp_value=>"xp_value * %xp_percentage_monsters% / 100"
  END

  LAUNCH_ACTION_FUNCTION edit_all_creatures
    STR_VAR
    edits	= "patch_data"
  END

  COPY_EXISTING_REGEXP "^.*\.bcs$" "override"
                       "^.*\.dlg$" "override"
    DECOMPILE_AND_PATCH
    BEGIN
      REPLACE_EVALUATE CASE_INSENSITIVE
        ~AddExperienceParty(\([0-9]+\))~
	BEGIN
	  SET xp = MATCH1 * xp_percentage_group / 100
	END
	~AddExperienceParty(%xp%)~

      REPLACE_EVALUATE CASE_INSENSITIVE
        ~AddXPObject(\(.+\), *\([0-9]+\))~
	BEGIN
	  SET xp = MATCH2 * xp_percentage_individual / 100
	END
	~AddXPObject(%MATCH1%, %xp%)~
    END

    BUT_ONLY_IF_IT_CHANGES


  COPY_EXISTING "xplist.2da" "override"
    COUNT_2DA_COLS cols
    COUNT_2DA_ROWS cols rows

    READ_2DA_ENTRIES_NOW __xplist_read cols

    FOR (SET i = 0; i < rows; ++i)
    BEGIN
      READ_2DA_ENTRY_FORMER __xplist_read i 2 old_xp

      INNER_PATCH_SAVE new_xp ~%old_xp%~
      BEGIN
        REPLACE_EVALUATE CASE_INSENSITIVE
          ~^\([0-9]+\)$~
	  BEGIN
	    SET xp = MATCH1 * xp_percentage_individual / 100
	  END
	  ~%xp%~

        REPLACE_EVALUATE CASE_INSENSITIVE
          ~^P_\([0-9]+\)$~
	  BEGIN
	    SET xp = MATCH1 * xp_percentage_group / 100
	  END
	  ~P_%xp%~
      END

      SET_2DA_ENTRY_LATER __xplist_set i 2 ~%new_xp%~
    END

    SET_2DA_ENTRIES_NOW __xplist_set cols
    PRETTY_PRINT_2DA

    BUT_ONLY_IF_IT_CHANGES
END	// adjust_xp_rewards


