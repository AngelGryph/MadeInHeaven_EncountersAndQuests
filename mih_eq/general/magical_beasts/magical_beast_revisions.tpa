DEFINE_ACTION_FUNCTION magical_beast_revisions
BEGIN
  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "wyvern_hd_bonus"
    RET
    wyvern_hd_bonus	= value
  END

  LAUNCH_ACTION_FUNCTION check_ini
    STR_VAR
    ini			= "wyvern_ac_bonus"
    RET
    wyvern_ac_bonus	= value
  END


  // Read in patching blocks

  ACTION_BASH_FOR "%MOD_FOLDER%/%component_loc%/patches" "^.*\.tph$"
  BEGIN
    INCLUDE "%BASH_FOR_FILESPEC%"
  END


  // Search and patch

  COPY_EXISTING_REGEXP "^.*\.cre$" "override"
    LAUNCH_PATCH_FUNCTION CRE_read_race
      RET
      race	= value
    END

    LAUNCH_PATCH_FUNCTION CRE_read_class
      RET
      class	= value
    END

    LAUNCH_PATCH_FUNCTION CRE_read_animation
      RET
      animation	= value
    END

    LAUNCH_PATCH_FUNCTION CRE_read_resist_magic
      RET
      magic_resistance	= value
    END

    PATCH_MATCH "%class%"
    WITH
      "ankheg" WHEN ("%race%" STRING_EQUAL_CASE "ankheg")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be an ankheg, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "ankheg"
        END
      END

      "basilisk_greater" WHEN ("%race%" STRING_EQUAL_CASE "basilisk")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a greater basilisk, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "greater_basilisk"
        END
      END

      "wolf_dread" WHEN ("%race%" STRING_EQUAL_CASE "wolf")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a dread wolf, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "dread_wolf"
        END
      END

      "wolf_winter" WHEN ("%race%" STRING_EQUAL_CASE "wolf")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a winter wolf, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "winter_wolf"
        END
      END

      "wyvern" WHEN ("%animation%" STRING_EQUAL_CASE "wyvern")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a baby wyvern, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "baby_wyvern"
        END
      END

      "wyvern" WHEN ("%animation%" STRING_EQUAL_CASE "wyvern_big" AND magic_resistance > 10)
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be a greater wyvern, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "greater_wyvern"
        END
      END

      "wyvern" WHEN ("%animation%" STRING_EQUAL_CASE "wyvern_big")
      BEGIN
        PATCH_PRINT "%SOURCE_RES% appears to be an adult wyvern, applying patches."
        LAUNCH_PATCH_FUNCTION apply_patches
	  STR_VAR
	  file_ext	= "CRE"
	  edits 	= "wyvern"
        END
      END
      DEFAULT
    END

    BUT_ONLY_IF_IT_CHANGES
END	// magical_beast_revisions


