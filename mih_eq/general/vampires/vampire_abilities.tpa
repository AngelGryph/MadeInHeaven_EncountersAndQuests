DEFINE_ACTION_FUNCTION vampire_abilities
BEGIN
  // Vampiric Rage innate

  LAUNCH_ACTION_FUNCTION install_spell
    STR_VAR
    spell	= "vampire_rage"
    idsname	= "VAMPIRE_RAGE"
    location	= "spells"
    editstring	= "say_name=>0"
  END


  // Vampire Rings
  //
  // Add magic damage resistance, +4 to invocation spells and turn immunity

  MAKE_PATCH
    delete_effect=>~opcode is_in [31 297 346 37]~
    add_effect_global_inline'1=>~opcode=>31 target=>1 timing=>2 parameter1=>50 parameter2=>1~
    add_effect_global_inline'2=>~opcode=>297 target=>1 timing=>2 parameter2=>1~
  END


  ACTION_IF ENGINE_IS "bgee bg2ee"
  BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data
    BEGIN
      add_effect_global_inline'3=>~opcode=>346 target=>1 timing=>2 parameter1=>4 mode=>6~
    END
  END
  ELSE
  BEGIN
    ACTION_DEFINE_ASSOCIATIVE_ARRAY patch_data
    BEGIN
      add_effect_global_inline'3=>~opcode=>37 target=>1 timing=>2 parameter1=>4~
    END
  END


  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item		= "vampreg vampreg1 vampreg2"
    edits		= "patch_data"
  END


  // Vampire Paws
  //
  // heal vampire 5 hp for every level drained

  MAKE_PATCH
    delete_effect=>~opcode = 17 and target = 1~
    clone_effect_inline=>~match=>"opcode = 216" opcode=>17 target=>1 parameter1=>"parameter1 * 5" parameter2=>0~
  END


  LAUNCH_ACTION_FUNCTION edit_item
    STR_VAR
    item		= "vamp vamp1 vamp2 vamp3 bodhi"
    edits		= "vampire_paws"
  END


  // Compile extra scripts

  LAUNCH_ACTION_FUNCTION install
    STR_VAR
    file	= "all"
    postfix	= "baf"
    location	= "scripts"
  END
END	// vampire_abilities


